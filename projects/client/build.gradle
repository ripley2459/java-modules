plugins {
    id "application"
    id "build-logic-app"
}

def module = "fr.cyrilneveu.client"

group = "fr.cyrilneveu"
version = "0.0.1"

dependencies {
    implementation project(":projects:vtk")
    implementation project(":projects:core")
    implementation project(":projects:common")
}

application {
    mainClass = "${module}.Main"
    mainModule = "${module}"
    // applicationDefaultJvmArgs = ["-Dgreeting.language=en"]
    // executableDir = "custom_bin_dir"
}

tasks.register("fatJarClient", Jar) {
    dependsOn ":projects:vtk:build"
    dependsOn ":projects:core:build"
    dependsOn ":projects:common:build"

    manifest {
        attributes "Main-Class": "${module}.Main"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } with jar
}

tasks.register("createClientJRE", Exec) {
    def jreFolder = "build/libs/jre"
    doFirst {
        def folder = file(jreFolder)
        if (folder.exists())
            delete folder
    }

    commandLine "jlink",
            "--module-path", "$System.env.JAVA_HOME/jmods",
            "--add-modules", 'java.base,java.desktop,java.se,java.xml,jdk.localedata',
            "--include-locales", "en,fr",
            "--output", "${jreFolder}"
}

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

tasks.register("createClientInstaller", Exec) {
    group = "DevOps"
    description = "Génère un .exe pour installer le projet."

    dependsOn ":projects:client:fatJarClient"
    dependsOn ":projects:client:createClientJRE"

    def timestamp = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMdd_HHmm"))
    commandLine "iscc",
            "/dMyAppVersion=${version}",
            "/dMyTimestamp=${timestamp}",
            "setup.iss"
}